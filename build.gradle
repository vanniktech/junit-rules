apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: 'com.vanniktech.android.junit.jacoco'

buildscript {
  ext.versions = [
    kotlin: '1.2.41',
    testRules: '1.0.1',
    espresso: '3.0.1',
  ]

  ext.deps = [
    'test': [
      'assertj2': "org.assertj:assertj-core:2.9.0",
      'assertj3': "org.assertj:assertj-core:3.9.1",
      'junit': 'junit:junit:4.12',
      'android': [
        'espresso': "com.android.support.test.espresso:espresso-core:${versions.espresso}",
        'runner': "com.android.support.test:runner:${versions.testRules}",
        'rules': "com.android.support.test:rules:${versions.testRules}",
      ],
    ],
    'kotlin': [
      'jdk7': "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}",
      'reflect': "org.jetbrains.kotlin:kotlin-reflect:${versions.kotlin}",
    ]
  ]

  repositories {
    mavenCentral()
    jcenter()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.10.0'
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.11.0'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
  }
}

codeQualityTools {
  checkstyle {
    toolVersion = '8.7'
  }
  pmd {
    toolVersion = '6.0.1'
  }
  detekt {
    toolVersion = '1.0.0.RC7'
  }
  ktlint {
    toolVersion = '0.23.1'
  }
  errorProne {
    toolVersion = '2.0.21'
  }
  lint {
    textReport = true
  }
}

subprojects {
  task allDependencies(type: DependencyReportTask) {}

  repositories {
    jcenter()
    google()
  }

  group = GROUP
  version = VERSION_NAME
}

ext {
  minSdkVersion = 15
  compileSdkVersion = 27

  javaVersion = JavaVersion.VERSION_1_7
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.5'
  distributionType = Wrapper.DistributionType.ALL
}
