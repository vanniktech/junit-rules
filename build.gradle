apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: 'com.vanniktech.android.junit.jacoco'

buildscript {
  ext.versions = [
    kotlin: '1.2.61',
    testRules: '1.0.2',
    espresso: '3.0.2',
  ]

  ext.deps = [
    'test': [
      'assertj2': "org.assertj:assertj-core:2.9.0",
      'assertj3': "org.assertj:assertj-core:3.11.1",
      'junit': 'junit:junit:4.12',
      'android': [
        'espresso': "com.android.support.test.espresso:espresso-core:${versions.espresso}",
        'runner': "com.android.support.test:runner:${versions.testRules}",
        'rules': "com.android.support.test:rules:${versions.testRules}",
      ],
    ],
    'kotlin': [
      'stdlib': "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
      'reflect': "org.jetbrains.kotlin:kotlin-reflect:${versions.kotlin}",
    ]
  ]

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.10.0'
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.12.0'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.5.0'
    classpath 'com.android.tools.build:gradle:3.1.4'
  }
}

codeQualityTools {
  checkstyle {
    toolVersion = '8.7'
  }
  pmd {
    toolVersion = '6.0.1'
  }
  detekt {
    toolVersion = '1.0.0.RC8'
  }
  ktlint {
    toolVersion = '0.27.0'
  }
  errorProne {
    toolVersion = '2.0.21'
  }
  lint {
    textReport = true
  }
}

subprojects {
  task allDependencies(type: DependencyReportTask) {}

  repositories {
    mavenCentral()
    google()
    jcenter()
  }
}

ext {
  minSdkVersion = 15
  compileSdkVersion = 28

  javaVersion = JavaVersion.VERSION_1_7
}

wrapper {
  gradleVersion = '4.8'
  distributionType = Wrapper.DistributionType.ALL
}
