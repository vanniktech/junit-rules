apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: 'com.vanniktech.android.junit.jacoco'

buildscript {
  ext.versions = [
    kotlin: '1.3.11',
    espresso: '3.1.1',
    testRules: '1.1.1',
  ]

  ext.deps = [
    'support': [
      'test': [
        'espresso': [
          'core': "androidx.test.espresso:espresso-core:${versions.espresso}",
        ],
        'junitExt': "androidx.test.ext:junit:1.0.0",
        'runner': "androidx.test:runner:${versions.testRules}",
        'rules': "androidx.test:rules:${versions.testRules}",
      ],
    ],
    'test': [
      'assertj2': "org.assertj:assertj-core:2.9.0",
      'assertj3': "org.assertj:assertj-core:3.11.1",
      'junit': 'junit:junit:4.12',
    ],
    'kotlin': [
      'stdlib': "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
      'reflect': "org.jetbrains.kotlin:kotlin-reflect:${versions.kotlin}",
    ]
  ]

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.15.0'
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.13.0'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.6.0'
    classpath 'com.android.tools.build:gradle:3.2.1'
  }
}

codeQualityTools {
  checkstyle {
    toolVersion = '8.7'
  }
  pmd {
    toolVersion = '6.0.1'
  }
  detekt {
    toolVersion = '1.0.0-RC12'
  }
  ktlint {
    toolVersion = '0.29.0'
  }
  errorProne {
    toolVersion = '2.0.21'
  }
  lint {
    textReport = true
  }
}

subprojects {
  task allDependencies(type: DependencyReportTask) {}

  repositories {
    mavenCentral()
    google()
    jcenter()
  }
}

ext {
  minSdkVersion = 15
  compileSdkVersion = 28

  javaVersion = JavaVersion.VERSION_1_7
}

wrapper {
  gradleVersion = '4.10.3'
  distributionType = Wrapper.DistributionType.ALL
}
